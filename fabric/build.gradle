plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    implementation "org.openjdk.nashorn:nashorn-core:${rootProject.nashorn_version}"
    include("org.openjdk.nashorn:nashorn-core:${rootProject.nashorn_version}")

    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${rootProject.terrablender_version}"
    modImplementation "com.github.glitchfiend:GlitchCore-fabric:${minecraft_version}-${rootProject.glitchcore_version}"
    modImplementation "com.github.glitchfiend:SereneSeasons-fabric:${minecraft_version}-${rootProject.serene_seasons_version}"
    modImplementation "com.github.glitchfiend:BiomesOPlenty-fabric:${minecraft_version}-${rootProject.bop_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothapi_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
}

processResources {
    var expandProps = [
            version: project.version,
            mod_name: rootProject.mod_name,
            mod_description: rootProject.mod_description,
            mod_author: rootProject.mod_author,
            mod_license: rootProject.mod_license,
            mod_issues_url: rootProject.mod_issues_url,
            mod_homepage_url: rootProject.mod_homepage_url
    ]

    inputs.properties(expandProps)

    filesMatching('fabric.mod.json') {
        expand expandProps
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
